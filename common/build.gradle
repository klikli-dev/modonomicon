plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
//    id 'org.spongepowered.gradle.vanilla'
    id 'fabric-loom'
}


if (System.getenv('MOD_VERSION') != null) {
    mod_version = System.getenv('MOD_VERSION')
}
version = mod_version

base {
    archivesName = "${mod_id}-${minecraft_version}-common"
}

loom {
    accessWidenerPath = file("src/main/resources/modonomicon.accesswidener")
    mixin {
        useLegacyMixinAp = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${loom_mapping_version}@zip")
    }

    compileOnly group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: "${jsr305_version}"

    //Jei
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"

    //commonmark
    //here we just need to add it to the classpaths, forge/fabric will put it in the jar for distribution
    implementation "org.commonmark:commonmark:${commonmark_version}"
    implementation "org.commonmark:commonmark-ext-gfm-strikethrough:${commonmark_version}"
    implementation "org.commonmark:commonmark-ext-ins:${commonmark_version}"
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()

            from components.java

            pom {
                name = mod_name
                description = 'Data-driven minecraft in-game documentation with progress visualization.'
                url = 'https://github.com/klikli-dev/modonomicon'
                licenses {
                    license {
                        name = 'MIT AND CC-BY-4.0'
                        url = 'https://github.com/klikli-dev/modonomicon#licensing'
                    }
                }
                scm {
                    connection = 'scm:git:ssh:git@github.com:klikli-dev/modonomicon.git'
                    url = 'https://github.com/klikli-dev/modonomicon'
                }
            }
        }
    }
    repositories {
        maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def releasesRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def snapshotsRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_PASS')
            }
        }
    }
}
